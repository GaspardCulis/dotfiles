# Do not modify this file!  It was generated by ‘nixos-generate-config’
# and may be overwritten by future invocations.  Please make changes
# to /etc/nixos/configuration.nix instead.
{
  pkgs,
  lib,
  config,
  modulesPath,
  ...
}: {
  imports = [
    (modulesPath + "/installer/scan/not-detected.nix")
    ../../../utils/allowed-unfree.nix
    ./disko-config.nix
  ];

  boot = {
    kernelPackages = lib.mkDefault pkgs.linuxPackages_zen;
    blacklistedKernelModules = ["nouveau"];
    kernelParams = [
      "mem_sleep_default=deep" # Should fix/change suspend method
      # Gayming performance
      "clocksource=tsc"
      "tsc=reliable"
      # These flags are used to enable backlight control when the dGPU is working in hybrid mode
      "i915.enable_dpcd_backlight=1"
      "nvidia.NVreg_EnableBacklightHandler=0"
      "nvidia.NVReg_RegistryDwords=EnableBrightnessControl=0"
    ];
    initrd = {
      availableKernelModules = [
        "nvme"
        "xhci_pci"
        "usbhid"
        "sdhci_pci"
      ];
      kernelModules = [];
    };
    loader = {
      timeout = 2;
      efi.canTouchEfiVariables = true;
      grub = {
        enable = true;
        devices = ["nodev"];
        efiSupport = true;
        useOSProber = true;
      };
    };
    tmp.useTmpfs = true;
    binfmt.emulatedSystems = ["aarch64-linux"];
  };

  # Network & Bluetooth
  networking.networkmanager.enable = true;
  networking.firewall.checkReversePath = "loose"; # For wireguard

  hardware.bluetooth.enable = true;
  hardware.bluetooth.powerOnBoot = true;
  # XBox wireless support
  hardware.xpadneo.enable = true;

  # Audio
  security.rtkit.enable = true;
  services.pipewire = {
    enable = true;
    alsa.enable = true;
    alsa.support32Bit = true;
    pulse.enable = true;
    # Fix pipewire battery drain (https://shorturl.at/G9iDS)
    wireplumber.extraConfig = {
      wireplumber.profiles = {
        main = {
          monitor.libcamera = "disabled";
        };
      };
    };
    # If you want to use JACK applications, uncomment this
    #jack.enable = true;
  };
  # fixes mic mute button
  services.udev.extraHwdb = ''
    evdev:name:*:dmi:bvn*:bvr*:bd*:svnASUS*:pn*:*
     KEYBOARD_KEY_ff31007c=f20
  '';

  hardware.graphics = {
    enable = true;
    enable32Bit = true;
  };
  hardware.amdgpu = {
    opencl.enable = true;
  };

  # NVIDIA
  services.xserver.videoDrivers = [
    "amdgpu"
    "nvidia"
  ];
  hardware.nvidia = {
    open = false;
    nvidiaSettings = true;
    modesetting.enable = true;
    dynamicBoost.enable = true;
    prime = {
      offload = {
        enable = true;
        enableOffloadCmd = true;
      };

      amdgpuBusId = "PCI:7:0:0";
      nvidiaBusId = "PCI:1:0:0";
    };
    package = config.boot.kernelPackages.nvidiaPackages.stable;
  };
  environment.systemPackages = with pkgs; [
    nvtopPackages.nvidia
    nvtopPackages.amd
  ];

  specialisation = {
    nvidia-sync.configuration = {
      system.nixos.tags = ["nvidia-sync"];
      hardware.nvidia = {
        prime.offload.enable = lib.mkForce false;
        prime.offload.enableOffloadCmd = lib.mkForce false;
        prime.sync.enable = lib.mkForce true;
      };
    };
  };

  # Enables DHCP on each ethernet and wireless interface. In case of scripted networking
  # (the default) this is the recommended approach. When using systemd-networkd it's
  # still possible to use this option, but it's recommended to use it in conjunction
  # with explicit per-interface declarations with `networking.interfaces.<interface>.useDHCP`.
  networking.useDHCP = lib.mkDefault true;
  # networking.interfaces.enp3s0.useDHCP = lib.mkDefault true;
  # networking.interfaces.enp7s0f4u2.useDHCP = lib.mkDefault true;
  # networking.interfaces.wlp4s0.useDHCP = lib.mkDefault true;

  nixpkgs.hostPlatform = lib.mkDefault "x86_64-linux";
}
